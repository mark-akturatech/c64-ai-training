# - Commented Commodore 64 KERNAL Disassembly (Magnus Nyman) - LOAD FROM SERIAL BUS (tape/RS232 path). Contains the high-level description and the code that prints 'SEARCHING', sends the filename and TALK/SA to the serial bus, receives the two-byte load address (<EAL and >EAL) via ACPTR ($EE13), and handles EOI (file not found). Implements relocated-load support by loading MEMUSS ($C3/$C4) into $AE/$AF when applicable. Enters the receive loop that: scans the <STOP> key ($FFE1), calls ACPTR to get bytes, checks STATUS for EOI/missing file, and either stores received bytes into memory (STA ($AE),Y) or compares them with memory (CMP ($AE),Y) when verifying (VERCK). The loop increments the low/high address ($AE/$AF), tests STATUS to continue or break, sends UNTALK, and performs end-of-routine branching and I/O error handling (JMP to error/finish addresses). Includes related JSRs used in this path.

                                *** LOAD FROM SERIAL BUS
                                The message 'SEARCHING' is printed and the filename is
                                sent with the TALK command and secondary address to the
                                serial bus. If EOI occurs at this point, then ?FILE NOT
                                FOUND is displayed. The message 'LOADING' or 'VERIFYING'
                                is output and a loop is entered, which receives a byte
                                from the serial bus, checks the <STOP> key and either
                                stores the received byte, or compares it to the memory,
                                depending on the state of VERCK. Finally the bus is
                                UNTALKed.
.,F4B6 90 7B    BCC $F533       device < 3, e.g. tape or RS232, illegal device
.,F4B8 A4 B7    LDY $B7         FNLEN, length of filename
.,F4BA D0 03    BNE $F4BF       if length not is zero
.,F4BC 4C 10 F7 JMP $F710       'MISSING FILENAME'
.,F4BF A6 B9    LDX $B9         SA, current secondary address
.,F4C1 20 AF F5 JSR $F5AF       print "SEARCHING"
.,F4C4 A9 60    LDA #$60
.,F4C6 85 B9    STA $B9         set SA to $60
.,F4C8 20 D5 F3 JSR $F3D5       send SA and filename
.,F4CB A5 BA    LDA $BA         FA, current devicenumber
.,F4CD 20 09 ED JSR $ED09       send TALK to serial bus
.,F4D0 A5 B9    LDA $B9         SA
.,F4D2 20 C7 ED JSR $EDC7       send TALK SA
.,F4D5 20 13 EE JSR $EE13       receive from serial bus
.,F4D8 85 AE    STA $AE         load address, <EAL
.,F4DA A5 90    LDA $90         check STATUS
.,F4DC 4A       LSR
.,F4DD 4A       LSR
.,F4DE B0 50    BCS $F530       EOI set, file not found
.,F4E0 20 13 EE JSR $EE13       receive from serial bus
.,F4E3 85 AF    STA $AF         load address, >EAL
.,F4E5 8A       TXA             retrieve SA and test relocated load
.,F4E6 D0 08    BNE $F4F0
.,F4E8 A5 C3    LDA $C3         use MEMUSS as load address
.,F4EA 85 AE    STA $AE         store in <EAL
.,F4EC A5 C4    LDA $C4
.,F4EE 85 AF    STA $AF         store in >EAL
.,F4F0 20 D2 F5 JSR $F5D2
.,F4F3 A9 FD    LDA #$FD        mask %11111101
.,F4F5 25 90    AND $90         read ST
.,F4F7 85 90    STA $90
.,F4F9 20 E1 FF JSR $FFE1       scan <STOP>
.,F4FC D0 03    BNE $F501       not stopped
.,F4FE 4C 33 F6 JMP $F633
.,F501 20 13 EE JSR $EE13      ACPTR, receive from serial bus
.,F504 AA       TAX
.,F505 A5 90    LDA $90
.,F507 4A       LSR
.,F508 4A       LSR
.,F509 B0 E8    BCS $F4F3
.,F50B 8A       TXA
.,F50C A4 93    LDY $93
.,F50E F0 0C    BEQ $F51C       jump to LOAD
.,F510 A0 00    LDY #$00
.,F512 D1 AE    CMP ($AE),Y     compare with memory
.,F514 F0 08    BEQ $F51E       verified byte OK
.,F516 A9 10    LDA #$10
.,F518 20 1C FE JSR $FE1C
.:F51B 2C       .BYTE $2C       mask next write command
.,F51C 91 AE    STA ($AE),Y     store in memory
.,F51E E6 AE    INC $AE         increment <EAL, next address
.,F520 D0 02    BNE $F524       skip MSB
.,F522 E6 AF    INC $AF         increment >EAL
.,F524 24 90    BIT $90         test STATUS
.,F526 50 CB    BVC $F4F3       get next byte
.,F528 20 EF ED JSR $EDEF       send UNTALK to serial bus
.,F52B 20 42 F6 JSR $F642
.,F52E 90 79    BCC $F5A9       end routine
.,F530 4C 04 F7 JMP $F704       I/O error #4, file not found


---
Additional information can be found by searching:
- "disk_and_device_specific_load_address_handling" which expands on Alternate device handling and relocated-load address computation (device >= 3)
