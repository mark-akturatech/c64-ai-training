# ca65 Users Guide - Detecting macro parameter types using .MATCH and token-list helpers. Example .macro ldax arg shows how to detect an immediate operand (#...) vs memory operand and emit appropriate lda/ldx sequences. Demonstrates use of .left, .right, .tcount and curly braces to pass token lists to pseudo functions. Usage examples: ldax #$1234 and ldax foo.

12.4 Detecting parameter types
Sometimes it is nice to write a macro that acts differently depending on the type of the argument supplied. An example would be a macro that loads a 16 bit value from either an immediate operand, or from memory. The .MATCH and .XMATCH functions will allow you to do exactly this:


.macro  ldax    arg
        .if (.match (.left (1, {arg}), #))
            ; immediate mode
            lda     #<(.right (.tcount ({arg})-1, {arg}))
            ldx     #>(.right (.tcount ({arg})-1, {arg}))
        .else
            ; assume absolute or zero page
            lda     arg
            ldx     1+(arg)
        .endif
.endmacro
Using the .MATCH function, the macro is able to check if its argument begins with a hash mark. If so, two immediate loads are emitted, Otherwise a load from an absolute zero page memory location is assumed. Please note how the curly braces are used to enclose parameters to pseudo functions handling token lists. This is necessary, because the token lists may include commas or parens, which would be treated by the assembler as end-of-list.

The macro can be used as


foo:    .word   $5678
...
        ldax    #$1234          ; X=$12, A=$34
...
        ldax    foo             ; X=$56, A=$78

---
Additional information can be found by searching:
- "macro_parameters_and_curly_braces" which expands on how token lists and curly braces affect parameters
