# - Fully Commented Commodore 64 KERNAL ROM Disassembly (English, "CBM") - Start of SERIAL4.0 library at $ED09: TALK/LISTN helpers to create talk/listen addresses by ORA #$40/#$20 respectively, protective call to RSP232 for RS232 NMI protection, LISTN handling that checks buffered chars (C3P0), sets EOI flags (R2D2), and calls ISOUR/ISOURA to send attention and data with proper IOCIA debouncing and clock toggling. Implements attention handling and clock/data toggling for the CBM serial bus.

                                       .BYTE <LINZ11
                                       .BYTE <LINZ12
                                       .BYTE <LINZ13
                                       .BYTE <LINZ14
                                       .BYTE <LINZ15
                                       .BYTE <LINZ16
                                       .BYTE <LINZ17
                                       .BYTE <LINZ18
                                       .BYTE <LINZ19
                                       .BYTE <LINZ20
                                       .BYTE <LINZ21
                                       .BYTE <LINZ22
                                       .BYTE <LINZ23
.:ECF0 00 28 50 78 A0 C8 F0 18         .BYTE <LINZ24
                                       .END
                                .LIB   SERIAL4.0
                                ;COMMAND SERIAL BUS DEVICE TO TALK
                                ;
.,ED09 09 40    ORA #$40        TALK   ORA #$40        ;MAKE A TALK ADR
.:ED0B 2C       .BYTE $2C       .BYT   $2C             ;SKIP TWO BYTES
                                ;COMMAND SERIAL BUS DEVICE TO LISTEN
                                ;
.,ED0C 09 20    ORA #$20        LISTN  ORA #$20        ;MAKE A LISTEN ADR
.,ED0E 20 A4 F0 JSR $F0A4       JSR    RSP232          ;PROTECT SELF FROM RS232 NMI'S
.,ED11 48       PHA             LIST1  PHA
                                ;
                                ;
.,ED12 24 94    BIT $94         BIT    C3P0            ;CHARACTER LEFT IN BUF?
.,ED14 10 0A    BPL $ED20       BPL    LIST2           ;NO...
                                ;
                                ;SEND BUFFERED CHARACTER
                                ;
.,ED16 38       SEC             SEC                    ;SET EOI FLAG
.,ED17 66 A3    ROR $A3         ROR    R2D2
                                ;
.,ED19 20 40 ED JSR $ED40       JSR    ISOUR           ;SEND LAST CHARACTER
                                ;
.,ED1C 46 94    LSR $94         LSR    C3P0            ;BUFFER CLEAR FLAG
.,ED1E 46 A3    LSR $A3         LSR    R2D2            ;CLEAR EOI FLAG
                                ;
                                ;
.,ED20 68       PLA             LIST2  PLA             ;TALK/LISTEN ADDRESS
.,ED21 85 95    STA $95         STA    BSOUR
.,ED23 78       SEI             SEI
.,ED24 20 97 EE JSR $EE97       JSR    DATAHI
.,ED27 C9 3F    CMP #$3F        CMP    #$3F            ;CLKHI ONLY ON UNLISTEN
.,ED29 D0 03    BNE $ED2E       BNE    LIST5
.,ED2B 20 85 EE JSR $EE85       JSR    CLKHI
                                ;
.,ED2E AD 00 DD LDA $DD00       LIST5  LDA D2PRA       ;ASSERT ATTENTION
.,ED31 09 08    ORA #$08        ORA    #$08
.,ED33 8D 00 DD STA $DD00       STA    D2PRA
                                ;
.,ED36 78       SEI             ISOURA SEI
.,ED37 20 8E EE JSR $EE8E       JSR    CLKLO           ;SET CLOCK LINE LOW
.,ED3A 20 97 EE JSR $EE97       JSR    DATAHI
.,ED3D 20 B3 EE JSR $EEB3       JSR    W1MS            ;DELAY 1 MS
.,ED40 78       SEI             ISOUR  SEI             ;NO IRQ'S ALLOWED
.,ED41 20 97 EE JSR $EE97              JSR DATAHI      ;MAKE SURE DATA IS RELEASED
.,ED44 20 A9 EE JSR $EEA9              JSR DEBPIA      ;DATA SHOULD BE LOW

---
Additional information can be found by searching:
- "serial_isour_isr" which expands on ISR routines and ISOUR used to send characters on the serial bus
