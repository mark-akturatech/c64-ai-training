# - Commented Commodore 64 KERNAL Disassembly (Magnus Nyman) - INPUT helper that supports reading from screen memory (as well as keyboard). Preserves X/Y, branches depending on CRSW to keyboard or screen input, reads character at current screen pointer PNT ($D1), applies quote tests and reverse (RVS) handling, increments PNTR, calls QUOTES TSET, and prepares return value (handling special 0xDE mapping to 0xFF). Also interacts with default I/O device flags (DFLTN/DFLTO) and calls screen output as needed. (Code: $E632..$E683).

                                *** INPUT FROM SCREEN OR KEYBOARD
                                This routine is used by INPUT to input data from devices
                                not on the serial bus, ie. from screen or keyboard. On
                                entry (X) and (Y) registers are preserved. A test is made
                                to determine which device the input is to be from. If it
                                is the screen, then quotes and <RVS> are tested for and
                                the character is echoed on the screen. Keyboard inputs
                                make use of the previous routine.
.,E632 98       TYA             preserve (X) and (Y) registers
.,E633 48       PHA
.,E634 8A       TXA
.,E635 48       PHA
.,E636 A5 D0    LDA $D0         CRSW, INPUT/GET from keyboard or screen
.,E638 F0 93    BEQ $E5CD       input from keyboard
.,E63A A4 D3    LDY $D3         PNTR, cursor column
.,E63C B1 D1    LDA ($D1),Y     read from current screen address
.,E63E 85 D7    STA $D7         temp store
.,E640 29 3F    AND #$3F
.,E642 06 D7    ASL $D7
.,E644 24 D7    BIT $D7
.,E646 10 02    BPL $E64A
.,E648 09 80    ORA #$80
.,E64A 90 04    BCC $E650
.,E64C A6 D4    LDX $D4         QTSW, editor in quotes mode
.,E64E D0 04    BNE $E654       yepp
.,E650 70 02    BVS $E654
.,E652 09 40    ORA #$40
.,E654 E6 D3    INC $D3         PNTR
.,E656 20 84 E6 JSR $E684       do quotes test
.,E659 C4 C8    CPY $C8         INDX, end of logical line for input
.,E65B D0 17    BNE $E674
.,E65D A9 00    LDA #$00
.,E65F 85 D0    STA $D0         CRSW
.,E661 A9 0D    LDA #$0D
.,E663 A6 99    LDX $99         DFLTN, default input device
.,E665 E0 03    CPX #$03        screen
.,E667 F0 06    BEQ $E66F       yes
.,E669 A6 9A    LDX $9A         DFLTO, default output device
.,E66B E0 03    CPX #$03        screen
.,E66D F0 03    BEQ $E672       yes
.,E66F 20 16 E7 JSR $E716       output to screen
.,E672 A9 0D    LDA #$0D
.,E674 85 D7    STA $D7
.,E676 68       PLA
.,E677 AA       TAX             restore (X) and (Y) registers
.,E678 68       PLA
.,E679 A8       TAY
.,E67A A5 D7    LDA $D7
.,E67C C9 DE    CMP #$DE
.,E67E D0 02    BNE $E682
.,E680 A9 FF    LDA #$FF
.,E682 18       CLC
.,E683 60       RTS


---
Additional information can be found by searching:
- "quotes_tset" which expands on calls QUOTES TSET to toggle quotes mode when reading a quote character
- "input_from_keyboard" which expands on keyboard path branches here when CRSW indicates keyboard input
