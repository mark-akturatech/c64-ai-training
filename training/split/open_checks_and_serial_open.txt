# - Fully Commented Commodore 64 KERNAL ROM Disassembly (English, "CBM") - Open path for serial and tape devices: after checking logical file entry, handles RS232 'talk to' sequences (ED09 TALK, TKATN/TKSA) and sends secondary addresses if present; for tape it calls cassette routines to search for or create files (CSTE1/CSTE2, FAF/FAH) and writes headers. Also prepares file buffer pointers and type flags for tape read/write.

.,F248 8A       TXA             JX350  TXA
.,F249 24 90    BIT $90         BIT    STATUS          ;DID HE LISTEN?
.,F24B 10 E6    BPL $F233       BPL    JX320           ;YES
                                ;
.,F24D 4C 07 F7 JMP $F707       JMP    ERROR5          ;DEVICE NOT PRESENT
                                ;***************************************
                                ;* CHKOUT -- OPEN CHANNEL FOR OUTPUT     *
                                ;*                                     *
                                ;* THE NUMBER OF THE LOGICAL FILE TO BE*
                                ;* OPENED FOR OUTPUT IS PASSED IN .X.  *
                                ;* CHKOUT SEARCHES THE LOGICAL FILE    *
                                ;* TO LOOK UP DEVICE AND COMMAND INFO. *
                                ;* ERRORS ARE REPORTED IF THE DEVICE   *
                                ;* WAS NOT OPENED FOR INPUT ,(E.G.     *
                                ;* KEYBOARD), OR THE LOGICAL FILE HAS   *
                                ;* REFERENCE IN THE TABLES.             *
                                ;* DEVICE 0, (KEYBOARD), AND DEVICE 3  *
                                ;* (SCREEN), REQUIRE NO TABLE ENTRIES  *
                                ;* AND ARE HANDLED SEPARATE.           *
                                ;***************************************
                                ;
.,F250 20 0F F3 JSR $F30F       NCKOUT JSR LOOKUP      ;IS FILE IN TABLE?
.,F253 F0 03    BEQ $F258       BEQ    CK5             ;YES...
                                ;
.,F255 4C 01 F7 JMP $F701       JMP    ERROR3          ;NO...FILE NOT OPEN
                                ;
.,F258 20 1F F3 JSR $F31F       CK5    JSR JZ100       ;EXTRACT TABLE INFO
                                ;
.,F25B A5 BA    LDA $BA         LDA    FA              ;IS IT KEYBOARD?
.,F25D D0 03    BNE $F262       BNE    CK10            ;NO...SOMETHING ELSE.
                                ;
.,F25F 4C 0D F7 JMP $F70D       CK20   JMP ERROR7      ;YES...NOT OUTPUT FILE
                                ;
                                ;COULD BE SCREEN,SERIAL,OR TAPES
                                ;
.,F262 C9 03    CMP #$03        CK10   CMP #3
.,F264 F0 0F    BEQ $F275       BEQ    CK30            ;IS SCREEN...DONE
.,F266 B0 11    BCS $F279       BCS    CK40            ;IS SERIAL...ADDRESS IT
.,F268 C9 02    CMP #$02        CMP    #2              ;RS232?
.,F26A D0 03    BNE $F26F       BNE    CK15
                                ;
.,F26C 4C E1 EF JMP $EFE1       JMP    CKO232
                                ;
                                ;
                                ;SPECIAL TAPE CHANNEL HANDLING
                                ;
.,F26F A6 B9    LDX $B9         CK15   LDX SA
.,F271 E0 60    CPX #$60        CPX    #$60            ;IS COMMAND READ?
.,F273 F0 EA    BEQ $F25F       BEQ    CK20            ;YES...ERROR
                                ;
.,F275 85 9A    STA $9A         CK30   STA DFLTO       ;ALL OUTPUT GOES HERE
                                ;
.,F277 18       CLC             CLC                    ;GOOD EXIT
.,F278 60       RTS             RTS
                                ;
.,F279 AA       TAX             CK40   TAX             ;SAVE DEVICE FOR DFLTO
.,F27A 20 0C ED JSR $ED0C       JSR    LISTN           ;TELL HIM TO LISTEN
                                ;
.,F27D A5 B9    LDA $B9         LDA    SA              ;IS THERE A SECOND?
.,F27F 10 05    BPL $F286       BPL    CK50            ;YES...

---
Additional information can be found by searching:
- "serial_command_and_listen" which expands on uses TALK/LISTN/TKATN/TKSA for serial device opening
- "jtget_and_cassette_buffers" which expands on tape header/search/write helpers FAF/FAH used here
