# - C64 I/O Map (Mapping The Commodore 64) - Sample BASIC program (with machine language subroutines) that switches the VIC-II to Bank 3, copies ROM character set to RAM, installs a small NMI handler to fix RESTORE, writes to new screen RAM area, erases and restores character set. Includes DATA for ML routines and POKE/SYS sequence demonstrating bank switching and character ROM handling.

                          Below is a sample program which switches to Bank 3.  It includes a
                          machine language transfer routine to move the ROM character set to
                          RAM, and a short interrupt routine to correct the RESTORE key problem.
                          After the switch is made, a loop is used to POKE characters into the
                          new screen memory area.  Next, the character data is slowly erased, to
                          show that the character set is now in RAM.  Then, a loop is used to
                          read the locations of the character set, and write to the same
                          locations.  This demonstrates that the 6510 reads the Kernal ROM when
                          you PEEK those locations, but POKEs to the RAM which is being
                          displayed.  Finally, the machine language move is used again to show
                          how quickly the set is restored.

                              20 FOR I=1 TO 33:READ A:POKE 49152+I,A:NEXT: REM SET UP ML ROUTINE
                              30 GOSUB 200: REM ML COPY OF ROM CHARACTER SET TO RAM
                              40 POKE 56576,PEEK(56576) AND 252: REM STEP 1, ENABLE BANK 3
                              50 POKE 53272,44: REM STEPS 2-3, POINT VIC-II TO SCREEN AND CHARACTER MEMORY
                              60 REM SCREEN OFFSET IS 2*16, CHARACTER OFFSET IS 12
                              70 POKE 648,200: REM STEP 4, POINT OS TO SCREEN AT 51200 (200*256)
                              80 PRINT CHR$(147): REM CLEAR SCREEN
                              90 FOR I=53236 TO 53245:READ A:POKE I,A:NEXT: REM NEW INTERRUPT ROUTINE
                              100 POKE 53246,PEEK(792):POKE 53247,PEK(793): REM SAVE OLD NMI VECTOR
                              110 POKE 792,244:POKE 793,207: REM ROUTE THE INTERRUPT THROUGH THE NEW ROUTINE
                              120 FOR I=0 TO 255:POKE 51400+I,I:POKE 55496+I,1:NEXT
                              125 REM POKE CHARACTERS TO SCREEN
                              130 FOR J=1 TO 8:FOR I=61439+J TO I+2048 STEP 8
                              140 POKE I,0:NEXT I,J: REM ERASE CHARACTER SET
                              150 FOR I=61440 TO I+2048:POKE I,PEEK(I):NEXT: REM POKE ROM TO RAM
                              160 GOSUB 200:END: REM RESTORE CHARACTER SET
                              200 POKE 56334,PEEK(56334) AND 254: REM DISABLE INTERRUPTS
                              210 POKE 1,PEEK(1) AND 251:REM SWITCH CHARACTER ROM INTO 6510 MEMORY
                              220 SYS 49152: REM COPY ROM CHARACTER SET TO RAM AT 61440
                              230 POKE 1,PEEK(1) OR 4: REM SWITCH CHARACTER ROM OUT OF 6510 MEMORY
                              240 POKE 56334,PEEK(56334)OR 1: REM ENABLE INTERRUPTS
                              250 RETURN
                              300 REM DATA FOR ML PROGRAM TO COPY CHARACTER SET TO RAM
                              310 DATA169,0,133,251,133,253,169,208,133,252,169,240,133,254,162,16
                              320 DATA160,0,177,251,145,253,136,208,249,230,252,230,254,202,208,240,96
                              330 REM NEXT IS ML PROGRAM TO MAKE THE RESTORE KEY RESET OS POINTER TO SCREEN
                              340 DATA 72,169,4,141,136,02,104,108,254,207

                          See also the sample program showing how to configure your 64 like a
                          PET at location 43 ($2B).


---
Additional information can be found by searching:
- "changing_vic_memory_banks_procedure" which expands on Implements the steps given in the procedure
- "character_generator_rom_overview_and_bit_values" which expands on Machine-language copy of character ROM to RAM explained
