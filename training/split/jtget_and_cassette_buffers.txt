# - Fully Commented Commodore 64 KERNAL ROM Disassembly (English, "CBM") - Cassette buffer retrieval (JTGET/JTP20/RBLK/WBLK): JTGET handles reading bytes from the TBUFFR cassette buffer (TAPE1/TAPE1 pointer), handles buffer wrap via JTP20, and calls RBLK to read next block if buffer empty. Also contains CASOUT handling to buffer output for tape write and checks for EOF markers and stop key during reads/writes.

.,F193 AA       TAX             JTG36  TAX             ;SAVE ERROR INFO
.,F194 68       PLA             PLA                    ;TOSS DATA
.,F195 8A       TXA             TXA                    ;RESTORE ERROR
.,F196 A6 97    LDX $97         JTG37  LDX XSAV        ;RETURN
.,F198 60       RTS             RTS                    ;ERROR RETURN C-SET FROM JTGET
                                ;GET A CHARACTER FROM APPROPRIATE
                                ;CASSETTE BUFFER
                                ;
.,F199 20 0D F8 JSR $F80D       JTGET  JSR JTP20       ;BUFFER POINTER WRAP?
.,F19C D0 0B    BNE $F1A9       BNE    JTG10           ;NO...
.,F19E 20 41 F8 JSR $F841       JSR    RBLK            ;YES...READ NEXT BLOCK
.,F1A1 B0 11    BCS $F1B4       BCS    BN33            ;STOP KEY PRESSED
.,F1A3 A9 00    LDA #$00        LDA    #0
.,F1A5 85 A6    STA $A6         STA    BUFPT           ;POINT TO BEGIN.
.,F1A7 F0 F0    BEQ $F199       BEQ    JTGET           ;BRANCH ALWAYS
                                ;
.,F1A9 B1 B2    LDA ($B2),Y     JTG10  LDA (TAPE1)Y    ;GET CHAR FROM BUF
.,F1AB 18       CLC             CLC                    ;GOOD RETURN
.,F1AC 60       RTS             RTS
                                ;INPUT FROM SERIAL BUS
                                ;
.,F1AD A5 90    LDA $90         BN30   LDA STATUS      ;STATUS FROM LAST
.,F1AF F0 04    BEQ $F1B5       BEQ    BN35            ;WAS GOOD
.,F1B1 A9 0D    LDA #$0D        BN31   LDA #$D         ;BAD...ALL DONE
.,F1B3 18       CLC             BN32   CLC             ;VALID DATA
.,F1B4 60       RTS             BN33   RTS
                                ;
.,F1B5 4C 13 EE JMP $EE13       BN35   JMP ACPTR       ;GOOD...HANDSHAKE
                                ;
                                ;INPUT FROM RS232
                                ;
.,F1B8 20 4E F1 JSR $F14E       BN50   JSR GN232       ;GET INFO
.,F1BB B0 F7    BCS $F1B4       BCS    BN33            ;ERROR RETURN
.,F1BD C9 00    CMP #$00        CMP    #00
.,F1BF D0 F2    BNE $F1B3       BNE    BN32            ;GOOD DATA...EXIT
.,F1C1 AD 97 02 LDA $0297       LDA    RSSTAT          ;CHECK FOR DSR OR DCD ERROR
.,F1C4 29 60    AND #$60        AND    #$60
.,F1C6 D0 E9    BNE $F1B1       BNE    BN31            ;AN ERROR...EXIT WITH C/R
.,F1C8 F0 EE    BEQ $F1B8       BEQ    BN50            ;NO ERROR...STAY IN LOOP
                                ;***************************************
                                ;* BSOUT -- OUT CHARACTER TO CHANNEL   *
                                ;*     DETERMINED BY VARIABLE DFLTO:   *
                                ;*     0 -- INVALID                    *
                                ;*     1 -- CASSETTE #1                *
                                ;*     2 -- RS232                      *
                                ;*     3 -- SCREEN                     *
                                ;*  4-31 -- SERIAL BUS                 *
                                ;***************************************
                                ;
.,F1CA 48       PHA             NBSOUT PHA             ;PRESERVE .A
.,F1CB A5 9A    LDA $9A         LDA    DFLTO           ;CHECK DEVICE
.,F1CD C9 03    CMP #$03        CMP    #3              ;IS IT THE SCREEN?
.,F1CF D0 04    BNE $F1D5       BNE    BO10            ;NO...
                                ;
                                ;PRINT TO CRT
                                ;
.,F1D1 68       PLA             PLA                    ;RESTORE DATA
.,F1D2 4C 16 E7 JMP $E716       JMP    PRT             ;PRINT ON CRT
                                ;
                                BO10

---
Additional information can be found by searching:
- "getin_and_basin" which expands on called when default input is cassette
