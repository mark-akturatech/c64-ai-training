# - Commented Commodore 64 KERNAL Disassembly (Magnus Nyman) - OUTPUT KERNAL ERROR MESSAGES: Centralized I/O error printing. Different entry points load an error number into (A) (e.g. #1..#9), then CLRCHN is called to close channels. If KERNAL messages enabled (MSGFLG $9D), prints 'I/O ERROR #' and converts numeric code to ASCII by ORA #$30 and outputs via CHROUT ($FFD2). Preserves stack entries around printing. Handles multiple preset error entry points via a sequence of LDA #$nn followed by JSR $FFCC and message printing.

                                *** OUTPUT KERNAL ERROR MESSAGES
                                The error message to be output is flagged into (A)
                                depending on the entry point. I/O channels are closed, and
                                then if KERNAL messages are enabled, "I/O ERROR #" is
                                printed along with the error number.
.,F6FB A9 01    LDA #$01        error #1, too many files
.:F6FD 2C       .BYTE $2C
.,F6FE A9 02    LDA #$02        error #2, file open
.:F700 2C       .BYTE $2C
.,F701 A9 03    LDA #$03        error #3, file not open
.:F703 2C       .BYTE $2C
.,F704 A9 04    LDA #$04        error #4, file not found
.:F706 2C       .BYTE $2C
.,F707 A9 05    LDA #$05        error #5, device not found
.:F709 2C       .BYTE $2C
.,F70A A9 06    LDA #$06        error #6, not input file
.:F70C 2C       .BYTE $2C
.,F70D A9 07    LDA #$07        error #7, not output file
.:F70F 2C       .BYTE $2C
.,F710 A9 08    LDA #$08        error #8, missing filename
.:F712 2C       .BYTE $2C
.,F713 A9 09    LDA #$09        error #9, illegal device number
.,F715 48       PHA
.,F716 20 CC FF JSR $FFCC       CLRCHN, close all I/O channels
.,F719 A0 00    LDY #$00
.,F71B 24 9D    BIT $9D         test MSGFLAG, KERNAL messages enabled
.,F71D 50 0A    BVC $F729       no
.,F71F 20 2F F1 JSR $F12F       print "I/O ERROR #"
.,F722 68       PLA
.,F723 48       PHA
.,F724 09 30    ORA #$30        convert (A) to ASCII number
.,F726 20 D2 FF JSR $FFD2       use CHROUT to print number in (A)
.,F729 68       PLA
.,F72A 38       SEC
.,F72B 60       RTS

---
Additional information can be found by searching:
- "clrchn_restore_default_io" which expands on used to close all channels before printing error messages
