# - Fully Commented Commodore 64 KERNAL ROM Disassembly (English, "CBM") - Wrap logic and line increment/decrement (WLOG/WLOG20..WLOG30): handles wrapping lines when inserting characters, auto-scroll-down behavior, adjusting line pointers (LDTB1 entries), checking maximum chars (MAXCHR), and routines to find the start (FINDST) or end (FNDEND) of physical lines. Also contains logic to call SCROL/CLRLN when needed.

                                WLOGIC
.,E6B6 20 B3 E8 JSR $E8B3              JSR CHKDWN      ;MAYBE WE SHOULD WE INCREMENT TBLX
.,E6B9 E6 D3    INC $D3                INC PNTR        ;BUMP CHARCTER POINTER
.,E6BB A5 D5    LDA $D5                LDA LNMX        ;
.,E6BD C5 D3    CMP $D3                CMP PNTR        ;IF LNMX IS LESS THAN PNTR
.,E6BF B0 3F    BCS $E700              BCS WLGRTS      ;BRANCH IF LNMX>=PNTR
.,E6C1 C9 4F    CMP #$4F               CMP #MAXCHR-1   ;PAST MAX CHARACTERS
.,E6C3 F0 32    BEQ $E6F7              BEQ WLOG10      ;BRANCH IF SO
.,E6C5 AD 92 02 LDA $0292              LDA AUTODN      ;SHOULD WE AUTO SCROLL DOWN?
.,E6C8 F0 03    BEQ $E6CD              BEQ WLOG20      ;BRANCH IF NOT
.,E6CA 4C 67 E9 JMP $E967       JMP    BMT1            ;ELSE DECIDE WHICH WAY TO SCROLL
                                WLOG20
.,E6CD A6 D6    LDX $D6                LDX TBLX        ;SEE IF WE SHOULD SCROLL DOWN
.,E6CF E0 19    CPX #$19               CPX #NLINES
.,E6D1 90 07    BCC $E6DA              BCC WLOG30      ;BRANCH IF NOT
.,E6D3 20 EA E8 JSR $E8EA              JSR SCROL       ;ELSE DO THE SCROL UP
.,E6D6 C6 D6    DEC $D6                DEC TBLX        ;AND ADJUST CURENT LINE#
.,E6D8 A6 D6    LDX $D6                LDX TBLX
.,E6DA 16 D9    ASL $D9,X       WLOG30 ASL LDTB1,X     ;WRAP THE LINE
.,E6DC 56 D9    LSR $D9,X              LSR LDTB1,X
.,E6DE E8       INX                    INX             ;INDEX TO NEXT LLINE
.,E6DF B5 D9    LDA $D9,X              LDA LDTB1,X     ;GET HIGH ORDER BYTE OF ADDRESS
.,E6E1 09 80    ORA #$80               ORA #$80        ;MAKE IT A NON-CONTINUATION LINE
.,E6E3 95 D9    STA $D9,X              STA LDTB1,X     ;AND PUT IT BACK
.,E6E5 CA       DEX                    DEX             ;GET BACK TO CURRENT LINE
.,E6E6 A5 D5    LDA $D5                LDA LNMX        ;CONTINUE THE BYTES TAKEN OUT
.,E6E8 18       CLC                    CLC
.,E6E9 69 28    ADC #$28               ADC #LLEN
.,E6EB 85 D5    STA $D5                STA LNMX
                                FINDST
.,E6ED B5 D9    LDA $D9,X              LDA LDTB1,X     ;IS THIS THE FIRST LINE?
.,E6EF 30 03    BMI $E6F4              BMI FINX        ;BRANCH IF SO
.,E6F1 CA       DEX                    DEX             ;ELSE BACKUP 1
.,E6F2 D0 F9    BNE $E6ED              BNE FINDST
                                FINX
.,E6F4 4C F0 E9 JMP $E9F0              JMP SETPNT      ;MAKE SURE PNT IS RIGHT
.,E6F7 C6 D6    DEC $D6         WLOG10 DEC TBLX
.,E6F9 20 7C E8 JSR $E87C              JSR NXLN
.,E6FC A9 00    LDA #$00               LDA #0
.,E6FE 85 D3    STA $D3                STA PNTR        ;POINT TO FIRST BYTE
.,E700 60       RTS             WLGRTS RTS
.,E701 A6 D6    LDX $D6         BKLN   LDX TBLX

---
Additional information can be found by searching:
- "editor_input_and_quote_handling" which expands on called after inserting characters to handle wrap/scroll
- "scrol_and_scrlin" which expands on screen scroll routines used here
