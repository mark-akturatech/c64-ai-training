# COMMODORE 64 - Sample BASIC program (tok64 page356.prg) demonstrating sending/receiving data to/from a modified Silent 700 terminal (PET ASCII). Includes OPEN of channel 2, enabling receiver (GET#), main loop reading keyboard and terminal (GET, GET#), sending with PRINT#2, reading and saving status with SR=ST, testing status bits and reporting parity/frame/receiver buffer full/break errors, waiting for transmission completion (PEEK 673), and CLOSE.


  SAMPLE BASIC PROGRAMS

start tok64 page356.prg
  10 rem this program sends and receives data to/from a silent 700
  11 rem terminal modified for pet ascii
  20 rem ti silent 700 set-up: 300 baud, 7-bit ascii, mark parity,
  21 rem full duplex
  30 rem same set-up at computer using 3-line interface
  100 open2,2,3,chr$(6+32)+chr$(32+128):rem open the channel
  110 get#2,a$:rem turn on the receiver channel (toss a null)
  200 rem main loop
  210 get b$:rem get from computer keyboard
  220 if b$<>""then print#2,b$;:rem if a key pressed, send to terminal
  230 get#2,c$:rem get a key from the terminal
  240 print b$;c$;:rem print all inputs to computer screen
  250 sr=st:ifsr=0orsr=8then200:rem check status, if good then continue
  300 rem error reporting
  310 print "error: ";
  320 if sr and 1 then print"parity"
  330 if sr and 2 then print"frame"
  340 if sr and 4 then print"receiver buffer full"
  350 if sr and 128 then print"break"
  360 if (peek(673)and1)then360:rem wait until all chars transmitted
  370 close 2:end
stop tok64

---
Additional information can be found by searching:
- "rs232_status_register_bits_and_usage" which expands on Uses SR=ST to test parity, framing, overrun and break bits
- "user_port_rs232_pin_assignments" which expands on Hardware modes used for 3-line RS-232 interface in the example
- "rs232_buffer_base_pointers" which expands on OPEN/CLOSE interactions with receiver/transmitter buffer base pointers
