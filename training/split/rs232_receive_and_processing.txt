# - Commented Commodore 64 KERNAL Disassembly (Magnus Nyman) - RS232 receive path and processing: RS232 RECEIVE (build input byte RIDATA under NMI, use INBIT/INPRTY flag handling), SET UP TO RECEIVE (configure CIA#2 ICR for edge detection and enable ENABL), PROCESS RS232 BYTE (parity checking, buffer storage, set RSSTAT on errors). Includes patch initialization to set RIPRTY on reception of start bit. Uses buffer pointers RIDBS/RIDBE ($029C/$029B) and RIBUF for storage.

                                *** RS232 RECEIVE
                                This routine builds up the input byte from the RS232 port
                                in RIDATA. Each bit is input from the port under NMI
                                interrupt control. The bit is placed in INBIT before being
                                passed to this routine, where it is shifted into the carry
                                flag and then rotated into RIDATA. The bit count is
                                decremented and parity updated.
.,EF59 A6 A9    LDX $A9         RINONE, check for start bit?
.,EF5B D0 33    BNE $EF90
.,EF5D C6 A8    DEC $A8         BITC1, RS232 in bit count
.,EF5F F0 36    BEQ $EF97       process received byte
.,EF61 30 0D    BMI $EF70
.,EF63 A5 A7    LDA $A7         INBIT, RS232 in bits
.,EF65 45 AB    EOR $AB         RIPRTY, RS232 in parity
.,EF67 85 AB    STA $AB
.,EF69 46 A7    LSR $A7         INBIT, put input bit into carry
.,EF6B 66 AA    ROR $AA         RIDATA,
.,EF6D 60       RTS
.,EF6E C6 A8    DEC $A8         BITC1
.,EF70 A5 A7    LDA $A7         INBIT
.,EF72 F0 67    BEQ $EFDB
.,EF74 AD 93 02 LDA $0293       M51CTR, 6551 control register image
.,EF77 0A       ASL
.,EF78 A9 01    LDA #$01
.,EF7A 65 A8    ADC $A8         BITC1
.,EF7C D0 EF    BNE $EF6D       end

                                *** SET UP TO RECEIVE
                                This routine sets up the I.C.R. to wait for the receiver
                                edge, and flags this into ENABL. It then flags the check
                                for a start bit.
.,EF7E A9 90    LDA #$90
.,EF80 8D 0D DD STA $DD0D       CIA#2 I.C.R.
.,EF83 0D A1 02 ORA $02A1       ENABL, RS232 enables
.,EF86 8D A1 02 STA $02A1
.,EF89 85 A9    STA $A9         RINONE, check for start bit
.,EF8B A9 02    LDA #$02
.,EF8D 4C 3B EF JMP $EF3B       disable timer and exit

                                *** PROCESS RS232 BYTE
                                The byte received from the RS232 port is checked against
                                parity. This involves checking the input parity options
                                selected, and then verifying the parity bit calculated
                                against that input. If the test is passed, then the byte
                                is stored in the in-buffer. Otherwise an error is flagged
                                into RSSTAT.
                                A patch in KERNAL version 3, has been added to the input
                                routine at $ef94 to initialise the RS232 parity byte,
                                RIPRTY, on reception of a start bit.
.,EF90 A5 A7    LDA $A7         INBIT, RS232 in bits
.,EF92 D0 EA    BNE $EF7E       set up to receive
.,EF94 4C D3 E4 JMP $E4D3       patch, init parity byte
.,EF97 AC 9B 02 LDY $029B       RIDBE, index to the end of in buffer
.,EF9A C8       INY
.,EF9B CC 9C 02 CPY $029C       RIDBS, start page of in buffer
.,EF9E F0 2A    BEQ $EFCA       receive overflow error
.,EFA0 8C 9B 02 STY $029B       RIDBE
.,EFA3 88       DEY
.,EFA4 A5 AA    LDA $AA         RIDATA, RS232 in byte buffer
.,EFA6 AE 98 02 LDX $0298       BITNUM, number of bits left to send
.,EFA9 E0 09    CPX #$09        full word to come?
.,EFAB F0 04    BEQ $EFB1       yes
.,EFAD 4A       LSR
.,EFAE E8       INX
.,EFAF D0 F8    BNE $EFA9
.,EFB1 91 F7    STA ($F7),Y     RIBUF, RS232 in buffer
.,EFB3 A9 20    LDA #$20
.,EFB5 2C 94 02 BIT $0294       M51CDR, 6551 command register image
.,EFB8 F0 B4    BEQ $EF6E       parity disabled
.,EFBA 30 B1    BMI $EF6D       parity check disabled, TRS
.,EFBC A5 A7    LDA $A7         INBIT, parity check
.,EFBE 45 AB    EOR $AB         RIPRTY, RS232 in parity
.,EFC0 F0 03    BEQ $EFC5       receive parity error
.,EFC2 70 A9    BVS $EF6D
.:EFC4 2C       .BYTE $2C       mask
.,EFC5 50 A6    BVC $EF6D
.,EFC7 A9 01    LDA #$01        receive parity error
.:EFC9 2C       .BYTE $2C       mask
.,EFCA A9 04    LDA #$04        receive overflow
.:EFCC 2C       .BYTE $2C       mask
.,EFCD A9 80    LDA #$80        framing break
.:EFCF 2C       .BYTE $2C       mask
.,EFD0 A9 02    LDA #$02        framing error
.,EFD2 0D 97 02 ORA $0297       RSSTAT, 6551 status register image
.,EFD5 8D 97 02 STA $0297
.,EFD8 4C 7E EF JMP $EF7E       set up to receive
.,EFDB A5 AA    LDA $AA         RIDATA
.,EFDD D0 F1    BNE $EFD0       framing error
.,EFDF F0 EC    BEQ $EFCD       receive break


---
Additional information can be found by searching:
- "rs232_control_and_delay" which expands on uses DELAY and GET SERIAL DATA helpers
- "rs232_send_receive_helpers" which expands on complements transmit-side routines (RODATA, RIBUF)
