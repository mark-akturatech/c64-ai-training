# - Fully Commented Commodore 64 KERNAL ROM Disassembly (English, "CBM") - GETIN and BASIN routines: GETIN dispatches input based on DFLTN (default input device). If device 0 (keyboard) it removes char from keyboard queue (LP2). If device 2 (RS232) it calls BSI232 to fetch from RS232 buffer. BASIN handles reading from keyboard (drives screen editor to produce whole lines), screen device handling (fake CR), and device >3 dispatch to serial bus/BASIN. Returns appropriate CR or buffer values and manages PNTR/TBLX preservation.

.,F14E 84 97    STY $97         GN232  STY XSAV        ;SAVE .Y, USED IN RS232
.,F150 20 86 F0 JSR $F086              JSR BSI232
.,F153 A4 97    LDY $97                LDY XSAV        ;RESTORE .Y
.,F155 18       CLC             GN20   CLC             ;GOOD RETURN
.,F156 60       RTS                    RTS
                                ;***************************************
                                ;* BASIN-- INPUT CHARACTER FROM CHANNEL*
                                ;*     INPUT DIFFERS FROM GET ON DEVICE*
                                ;* #0 FUNCTION WHICH IS KEYBOARD. THE  *
                                ;* SCREEN EDITOR MAKES READY AN ENTIRE *
                                ;* LINE WHICH IS PASSED CHAR BY CHAR   *
                                ;* UP TO THE CARRIAGE RETURN.  OTHER   *
                                ;* DEVICES ARE:                        *
                                ;*      0 -- KEYBOARD                  *
                                ;*      1 -- CASSETTE #1               *
                                ;*      2 -- RS232                     *
                                ;*      3 -- SCREEN                    *
                                ;*   4-31 -- SERIAL BUS                *
                                ;***************************************
                                ;
.,F157 A5 99    LDA $99         NBASIN LDA DFLTN       ;CHECK DEVICE
.,F159 D0 0B    BNE $F166       BNE    BN10            ;IS NOT KEYBOARD...
                                ;
                                ;INPUT FROM KEYBOARD
                                ;
.,F15B A5 D3    LDA $D3         LDA    PNTR            ;SAVE CURRENT...
.,F15D 85 CA    STA $CA         STA    LSTP            ;... CURSOR COLUMN
.,F15F A5 D6    LDA $D6         LDA    TBLX            ;SAVE CURRENT...
.,F161 85 C9    STA $C9         STA    LSXP            ;... LINE NUMBER
.,F163 4C 32 E6 JMP $E632       JMP    LOOP5           ;BLINK CURSOR UNTIL RETURN
                                ;
.,F166 C9 03    CMP #$03        BN10   CMP #3          ;IS INPUT FROM SCREEN?
.,F168 D0 09    BNE $F173       BNE    BN20            ;NO...
                                ;
.,F16A 85 D0    STA $D0         STA    CRSW            ;FAKE A CARRIAGE RETURN
.,F16C A5 D5    LDA $D5         LDA    LNMX            ;SAY WE ENDED...
.,F16E 85 C8    STA $C8         STA    INDX            ;...UP ON THIS LINE
.,F170 4C 32 E6 JMP $E632       JMP    LOOP5           ;PICK UP CHARACTERS
                                ;
.,F173 B0 38    BCS $F1AD       BN20   BCS BN30        ;DEVICES >3
.,F175 C9 02    CMP #$02        CMP    #2              ;RS232?
.,F177 F0 3F    BEQ $F1B8       BEQ    BN50
                                ;
                                ;INPUT FROM CASSETTE BUFFERS
                                ;
.,F179 86 97    STX $97         STX    XSAV
.,F17B 20 99 F1 JSR $F199       JSR    JTGET
.,F17E B0 16    BCS $F196       BCS    JTG37           ;STOP KEY/ERROR
.,F180 48       PHA             PHA
.,F181 20 99 F1 JSR $F199       JSR    JTGET
.,F184 B0 0D    BCS $F193       BCS    JTG36           ;STOP KEY/ERROR
.,F186 D0 05    BNE $F18D       BNE    JTG35           ;NOT AN END OF FILE
.,F188 A9 40    LDA #$40        LDA    #64             ;TELL USER EOF
.,F18A 20 1C FE JSR $FE1C       JSR    UDST            ;IN STATUS
.,F18D C6 A6    DEC $A6         JTG35  DEC BUFPT
.,F18F A6 97    LDX $97         LDX    XSAV            ;.X PRESERVED
.,F191 68       PLA             PLA                    ;CHARACTER RETURNED
                                ;C-CLEAR FROM JTGET
.,F192 60       RTS             RTS                    ;ALL DONE
                                ;

---
Additional information can be found by searching:
- "keyboard_scan_and_irq_handling" which expands on keyboard queue removal uses LP2/LP1 code
- "rs232inout_buffer_and_protection" which expands on calls BSI232 for RS232 input
